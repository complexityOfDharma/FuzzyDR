FUNCTION_BLOCK delta_external 

VAR_INPUT
    networkState : REAL;
    	RANGE := (-100 .. 100);
    actionConsensus : REAL;
    	RANGE := (0 .. 10);
END_VAR

VAR_OUTPUT
    delta_e : REAL;
    	RANGE := (0 .. 1);
END_VAR

FUZZIFY networkState
    TERM advantaged := (0, 0) (100, 1);
    TERM parity := trian -50 0 50;
    TERM disadvantaged := (-100, 1) (0, 0);
END_FUZZIFY

FUZZIFY actionConsensus
    TERM conforming := (0, 1) (10, 0);
    TERM deviating := (0, 0) (10, 1);
END_FUZZIFY

DEFUZZIFY delta_e
    TERM embrace := (0.4, 0) (0.8, 1) (1, 1);
    TERM reject := (0, 1) (0.2, 1) (0.6, 0);
    METHOD: COG;
END_DEFUZZIFY

RULEBLOCK No1
	AND : MIN;	// Use 'min' for 'and'
	ACT : MIN;	// Use 'min' activation method
	ACCU : MAX;	// Use 'max' accumulation method

	RULE 1: IF networkState IS advantaged AND actionConsensus IS conforming THEN delta_e IS embrace;
	RULE 2: IF networkState IS advantaged AND actionConsensus IS deviating THEN delta_e IS reject;
	RULE 3: IF networkState IS parity THEN delta_e IS embrace;
	RULE 4: IF networkState IS disadvantaged THEN delta_e IS embrace;

END_RULEBLOCK

END_FUNCTION_BLOCK